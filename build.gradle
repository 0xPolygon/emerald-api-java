buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.jfrog.bintray'

group = 'io.emeraldpay'
version = '0.1'

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

sourceSets {
    main {
        proto {
            srcDir '../emerald-grpc/proto'
        }
    }
}

dependencies {
    api "io.grpc:grpc-protobuf:${grpcVersion}"
    api "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.netty:netty-tcnative-boringssl-static:2.0.22.Final"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'


    testImplementation "org.codehaus.groovy:groovy:${groovyVersion}"
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

compileJava.dependsOn("generateProto")

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "splix"
            name "Igor Artamonov"
            email "igor@artamonov.ru"
        }
    }

    scm {
        connection "scm:git:git://github.com/etcdevteam/emerald-grpc-java.git"
        developerConnection "scm:git:git://github.com/etcdevteam/emerald-grpc-java.git"
        url "https://github.com/etcdevteam/emerald-grpc-java"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        EmeraldPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Emerald gRPC Java Stubs')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintrayUser') ? System.getProperty('bintrayUser') : System.getenv('BINTRAY_USER')
    key = System.getProperty('bintrayApiKey') ? System.getProperty('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['EmeraldPublication']

    dryRun=false
    publish=true
    override=true

    pkg {
        userOrg = 'emerald'
        repo = 'emerald-grpc'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/etcdevteam/emerald-grpc-java.git'
        labels = ['ethereum', 'bitcoin', 'blockchain', 'java-8', 'grpc']
        publicDownloadNumbers = true

        version {
            name = project.version
            description = 'Emerald gRPC Java Stubs'
            released  = new Date()
            vcsTag = project.version

            gpg {
                sign = true
            }
        }
    }
}
